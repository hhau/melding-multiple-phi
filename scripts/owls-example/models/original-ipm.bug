# From: Estimation of immigration rate using integrated population models
# doi: 10.1111/j.1365-2664.2010.01789.x

model {
 #*****************************************
 # 5.1 Define the regression equations
 #*****************************************
  for (i in 1 : (ti - 1)) {
    # Recapture rate
    # Model 4: Finke, only capture probabilities are time varying.
    logit(pM[i]) <- v[5] + bp[i]  # Male
    logit(p[i]) <- bp[i]          # Female
  }

  # Juvenile survival rate
  logit(phijM) <- v[1] + v[3]  # Male
  logit(phij) <- v[1]          # Female

  # Adult survival
  logit(phiaM) <- v[1] + v[2] + v[3]    # Male
  logit(phia) <- v[1] + v[2]            # Female

  # Immigration rate
  log(im) <- v[6]

  #*******************************************
  # 5.2 Define the priors for the parameters
  #*******************************************
  for (i in 1 : 6) {
    v[i] ~ dnorm(0, 0.25) T(-10, 10)
  }
  # Wed 11 Mar 14:54:05 2020
  # change to constant rho/fec
  fec ~ dunif(0, 10)

  for (i in 1 : (ti - 1)) {
    bp[i] ~ dnorm(0, 0.25) T(-10, 10)
  }

  # Wed  4 Mar 16:57:25 2020
  # Doing it properly, and aligning with Finke:
  for (ii in 1 : 50) {
    flat_p[ii] <- 1 / 50
  }
  N1[1] ~ dcat(flat_p[1 : 50])      # 1-year
  NadSurv[1] ~ dcat(flat_p[1 : 50]) # Adults
  Nadimm[1] ~ dcat(flat_p[1 : 50])  # Immigrants

  #*****************************************
  # 5.4 The Integrated population model
  #******************************************

  #**************************************************
  # 5.4.1 Likelihood for reproductive data
  #**************************************************
  for (i in 1 : (ti - 1)) {
    nestlings[i] ~ dpois(rho[i])
    rho[i] <- sample.size[i] * fec
  }

  #***********************************************
  # 5.4.2 Likelihood for population survey data
  #************************************************
  #***************************
  # System process
  #***************************
  ## Fri 27 Mar 17:37:56 2020
  ## 'mean1' and 'mpo' are incorrectly indexed, this creates
  ## nodes 'mean1[1]' and 'mpo[1]' which do not ever get
  ## computed .This is not a problem from a BUGS/Jags point
  ## of view, the nodes never contribute to the log-prob
  ## computation. But it makes for a lot of error messages.
  for (tt in 2 : ti) {
    mean1[tt] <- 0.5 * fec * phij * Ntot[tt - 1]
    N1[tt] ~ dpois(mean1[tt])
    mpo[tt] <- Ntot[tt - 1] * im
    NadSurv[tt] ~ dbin(phia, Ntot[tt - 1])
    Nadimm[tt] ~ dpois(mpo[tt])
  }

  #*****************************
  # Observation process
  #*****************************
  for (tt in 1 : ti) {
    Ntot[tt] <- NadSurv[tt] + Nadimm[tt] + N1[tt]
    popcount[tt] ~ dpois(Ntot[tt])
  }

  #***********************************************************
  # 5.4.3 Likelihood for capture-recapture data:CJS models (2 age classes)
  #***********************************************************
  #***********************************
  # Female capture recapture data
  #***********************************
  for (i in 1 : (2 * (ti - 1))) {
    m[i, 1 : ti] ~ dmulti(pr[i, 1 : ti], r[i])
  }

  # m-array cell probabilities for juveniles
  for (i in 1 : (ti - 1)) {
    q[i] <- 1 - p[i]

    # Main diagonal
    pr[i, i]<- phij * p[i]

    # above main diagonal
    for (j in (i + 1) : (ti - 1)) {
      pr[i, j] <- phij * phia * prod(q[i : (j - 1)]) * p[j]
    }

    # Below main diagonal
    for (j in 1 : (i - 1)) {
      pr[i, j] <- 0
    }

    # Last column
    pr[i, ti] <- 1 - sum(pr[i, 1 : (ti - 1)])
  }

  # m-array cell probabilities for adults
  for (i in 1 : (ti - 1)) {
    # main diagonal
    pr[i + ti - 1, i] <- phia * p[i]

    # above main diagonal
    for (j in (i + 1) : (ti - 1)) {
      pr[i + ti - 1, j] <- phia * prod(q[i : (j - 1)]) * p[j]
    }

    # below main diagonal
    for (j in 1 : (i - 1)) {
      pr[i + ti - 1, j] <- 0
    }

    # last column
    pr[i + ti - 1, ti] <- 1 - sum(pr[i + ti - 1, 1 : (ti - 1)])
  }
  #*********************************
  # Male capture recapture data
  #*********************************
  for (i in 1 : (2 * (ti - 1))) {
    mM[i, 1 : ti] ~ dmulti(prM[i, 1 : ti], rM[i])
  }

  # m-array cell probabilities for juveniles
  for (i in 1 : (ti - 1)) {
    qM[i] <- 1 - pM[i]

    # main diagonal
    prM[i, i] <- phijM * pM[i]

    # above main diagonal
    for (j in (i + 1) : (ti - 1)) {
      prM[i, j] <- phijM * phiaM * prod(qM[i : (j - 1)]) * pM[j]
    }

    # below main diagonal
    for (j in 1 : (i - 1)) {
      prM[i, j] <- 0
    }

    # last column
    prM[i, ti] <- 1 - sum(prM[i, 1 : (ti - 1)])
  }

  # m-array cell probabilities for adults
  for (i in 1 : (ti - 1)) {
    # main diagonal
    prM[i + ti - 1, i] <- phiaM * pM[i]

    # above main diagonal
    for (j in (i + 1) : (ti - 1)) {
      prM[i + ti - 1, j] <- phiaM * prod(qM[i : (j - 1)]) * pM[j]
    }

    # below main diagonal
    for (j in 1 : (i - 1)) {
      prM[i + ti - 1, j] <- 0
    }

    # last column
    prM[i + ti - 1, ti] <- 1 - sum(prM[i + ti - 1, 1 : (ti - 1)])
  }
}
