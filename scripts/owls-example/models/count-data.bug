# From: Estimation of immigration rate using integrated population models
# doi: 10.1111/j.1365-2664.2010.01789.x

model {
 #*****************************************
 # 5.1 Define the regression equations
 #*****************************************
  ## ------- \phi_{1 \cap 2}
  # Juvenile survival rate
  logit(phij) <- v[1]          

  ## ------- \phi_{1 \cap 2}
  # Adult survival
  logit(phia) <- v[1] + v[2]

  ## ------- \psi_{2}^{par}
  # Immigration rate
  im <- v[6]

  #*******************************************
  # 5.2 Define the priors for the parameters
  #*******************************************
  for (i in 1 : 6) {
    v[i] ~ dnorm(0, 0.0001) T(-10, 10)
  } 
  # Wed 11 Mar 14:54:05 2020 
  # change to constant rho/fec
  ## ------- \phi_{2 \cap 3}
  fec ~ dunif(0, 10)
  
  # Wed  4 Mar 16:57:25 2020
  # Doing it properly, and aligning with Finke:
  ## ------- \psi_{2}^{lat}
  for (ii in 1 : 50) {
    flat_p[ii] <- 1 / 50
  } 
  N1[1] ~ dcat(flat_p[1 : 50])
  NadSurv[1] ~ dcat(flat_p[1 : 50]) 
  Nadimm[1] ~ dcat(flat_p[1 : 50])  

  #***********************************************
  # 5.4.2 Likelihood for population survey data
  #************************************************
  #***************************
  # System process
  #***************************
  ## ------- \psi_{2}^{lat}
  for (tt in 2 : ti) {
    mean1[tt] <- 0.5 * fec * phij * Ntot[tt - 1]
    N1[tt] ~ dpois(mean1[tt])
    mpo[tt] <- Ntot[tt - 1] * im
    NadSurv[tt] ~ dbin(phia, Ntot[tt - 1])
    Nadimm[tt] ~ dpois(mpo[tt])
  }

  #*****************************
  # Observation process
  #*****************************
  ## ------- \psi_{2}^{lat}
  for (tt in 1 : ti) {
    Ntot[tt] <- NadSurv[tt] + Nadimm[tt] + N1[tt]
    popcount[tt] ~ dpois(Ntot[tt])
  }
              
}
